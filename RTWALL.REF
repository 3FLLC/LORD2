@DES `0T`2he `0R`2T `0W`2all
@NOCHECK

RT Wall : FREE-SCRIPT-WARE.
(c) Copyright 1995: Robinson Technologies
Created by Seth A. Robinson

** READ THIS CRAP!  THERE ARE SETTINGS YOU CAN SET BELOW! **

A few sysops like the cut of my chatwalls jibe so I've added a thing or
two and released it to the public.

  * Nice looking chat wall.
  * Definable Top line, Lines the chat wall should be, and filename to save
    data file as.
  * Option to NOT ALLOW BBS # posting.  It inforces it also.
  * Changes swear words to 'other words' using the REPLACE command, you
    can find it below, and cut, paste and edit more if needed.
  * Supports LORD color codes.
  * Every aspect can be changed, added to - Read REFHELP.DOC included with
    RTR (RTReader) for info on understand .REF script files.
  * Good God man!  It's a completely free quality piece of work!

°±² QUICK START ²±°

  Well, you need RTREAD02.ZIP or newer.  You should read it's docs, but
  basically, here is how you would do it:

  START <node num> RTWALL.REF

  This forces RTREADER to run RTWALL.REF.

  Be sure to put the path before RTWALL.REF if you have it in a
  different directory.  If you put in the same dir as RTR and run RTR
  with no parms, it will show up on the 'pick list'.


HERE ARE THE PARMS YOU CAN SET: (note:  `x means a space, the `c clears screen)

@#START
@VERSION 2

Name of wall---------------
                         /|\
@DO `s01 is `x`x`x`x`x`x`x`x`x`x`x`x`x`x`x`x`x`x`x`x`x`1°±²Û`r1`%`x`xThe Darkside Chatter`x`x`r0`1Û²±°

Filename of wall text file
                  |
                 /|\
@DO `s02 is CHATTER.DAT

@DO `p01 is 20 <-Set this to max lines of wall

@DO `p02 is 0 <-- BBS Numbers allowed?  (0 = no, 1 = yeah)


Thats it!  Now the program starts.

@DO WRITE
`c`s01
@SHOW

@IF `s02 EXIST TRUE then do
  @BEGIN
   @DISPLAYFILE `s02
  @END

@IF `s02 EXIST FALSE then do
  @BEGIN
   @show

`2The wall seems to be unused at this time.
  @END
@SHOW

@DO WRITE
`0Add something to the conversation?`x
@DO READSTRING 1 Y `s03
@SHOW

@DO upcase `s03
@IF `s03 is N then do
  @BEGIN
  @DO `p03 RANDOM 5 1

  (the following are random statements it gives if they DON'T wanna add)

  (you could easily add more, just add 'em at the bottom, and up the
   random command above)

  @IF `p03 is 1 then `s03 is `0What's`xthe`xmatter?`x`xMommy`xain't`xhere`xto`xhelp?!`xHaw!
  @IF `p03 is 2 then `s03 is `bCoward!
  @IF `p03 is 3 then `s03 is `0My,`xaren't`xwe`xdaring`xand`xbold.
  @IF `p03 is 4 then `s03 is `0Communication`xis`xnot`xsuch`xa`xbad`xthing!
  @IF `p03 is 5 then `s03 is `0Try`xit`xnext`xtime!`x`xIt's`xkinda`xfun.
  @SHOW

`s03
  @CLOSESCRIPT
  @END
  @IF `p02 is 0 then do
  @BEGIN
  @SHOW

`0Enter your line of text.  `%(No BBS advertising please!)

  @END
  @IF `p02 is 1 then do
  @BEGIN
  @SHOW

`0Enter your line of text.

  @END

(Do tricky stuff with name to determine how much they can type)
@DO `s04 is `%
@DO `s04 add `n
@DO STRIP `s04 <-- Strips all none color ` codes out
@DO `s04 add `x`2:`x`0
@DO `p04 is length `s04
@DO `p05 is 76
@DO `p05 - `p04
@DO WRITE
`s04
@DO READSTRING `p05 NIL `s03 <--Read string, NIL is default. (nothing)
@SHOW

@DO `p04 is length `s03
@IF `p04 less 3 then do
 @begin
 @SHOW

`0Sorry.  That is just too stupid to save.

 @DO GOTO DONE
 @END
 
 @DO STRIPBAD `s03
 @IF `p02 is 0 then do
   @BEGIN
   @DO NUMRETURN `p05 `s03
   @IF `p05 more 6 then do
    @BEGIN
    @SHOW

`0You sir, are a bastard.  `%NO FRIGGING BBS NUMBERS!`0

    @DO GOTO DONE
    @END
    @END
  
  (the following checks for 'bad words' and replaces them with 'no so bad
  words')  (feel free to add/change - it's kind of fun ;> )

 @DO REPLACEALL shit shoot `s03
 @DO REPLACEALL fuck fudge `s03
 @DO REPLACEALL phuck darn `s03
 @DO REPLACEALL hate love `s03  <-- This is so frigging funny in action!
 @WRITEFILE `s02
`s04`s03
 @DO TRIM `s02 `p01 <-- Trims file to max lines
 @SHOW

`0Message saved.

 @end
 @LABEL DONE
@CLOSESCRIPT

